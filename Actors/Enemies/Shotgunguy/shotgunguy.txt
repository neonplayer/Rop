/*

shrapnel sprites: ID Software, Raven Software, 3D Realms, Nanami, Eriance, Keksdose, Jimmy91(Jimmy?).

*/

actor ROPshotgunguy : shotgunguy replaces shotgunguy
{
  painchance 128
  Obituary "%o got some buckshot from a shotgun zombie."
  tag "Shotgun Guy"
  states
  {
  spawn:
    SPOS AB 10 a_look
	loop
  Missile:
    SPOS E 10 A_FaceTarget
	SPOS A 0 a_jumpiftargetinlos("bulletmissile")
	SPOS F 0 a_playsound("shotguy/attack")
	SPOS FF 0 A_custommissile("shotguyshrapnel", 32, 0, random(50, -50)*0.1, 0, random(50, -50)*0.1)
    SPOS F 10 Bright A_custommissile("shotguyshrapnel", 32, 0, random(50, -50)*0.1, 0, random(50, -50)*0.1)
    SPOS E 10
    Goto See
  bulletmissile:
    SPOS F 10 Bright A_SPosAttackUseAtkSound
    SPOS E 10
    Goto See
  XDeath:
    SPOS M 5 a_giveinventory("gibtoken", 1)
    SPOS N 5 A_XScream
    SPOS O 5 A_NoBlocking
    SPOS PQRST 5
    SPOS U -1
    Stop
  Raise:
    SPOS A 0 a_jumpifinventory("gibtoken", 1, "xraise")
    SPOS L 5
    SPOS KJIH 5
    Goto See
  xraise:
    SPOS A 0 a_takeinventory("gibtoken", 1)
	SPOS UTSRQPONM 5 
	goto see
  }
}

actor possessedROPshotgunguy : possessedmonster
{
  player.morphweapon "ROPzombieshotgun"
  health 30
  Radius 20
  Height 56
  Mass 100
  Speed 0.4
  PainChance 170
  +FLOORCLIP
  SeeSound "shotguy/sight"
  PainSound "shotguy/pain"
  DeathSound "shotguy/death"
  ActiveSound "shotguy/active"
  Obituary "%o got some buckshot from %k."
  Player.SoundClass Zombie
  tag "Shotgun Guy"
  States
  {
  Spawn:
    SPOS AB 10
	loop
  See:
    SPOS AABBCCDD 3
    goto spawn
  Missile:
    SPOS F 6 bright
    SPOS E 41
    Goto spawn
  Pain:
    SPOS G 3
    SPOS G 3 A_Pain
    Goto spawn
  }
}

actor ROPzombieshotgun : possessedweapon
{
  decal bulletchip
  weapon.bobstyle inversesmooth
  weapon.bobrangex 0.5
  weapon.bobrangey 0.75
  weapon.bobspeed 1.25
  tag "Zombie Shotgun"
  states
  {
  ready:
    ZMBS A 1 a_weaponready
	loop
  fire:
    ZMBS A 0 a_playweaponsound("shotguy/attack")
	ZMBS AAAA 0 a_firecustommissile("bulletparticle", random(20, -20)*0.1, 0, 0, 0, 0, random(20, -20)*0.1)
    ZMBS B 1 bright a_firebullets(5, 5, 5, 10, "monsterbulletpuff", fbf_norandom)
	ZMBS C 1 bright
	ZMBS DEFF 1
	ZMBS EGDHIJKLMNA 1
	ZMBS OPQRSTUVWXYZ 1
	ZMS2 A 3
	ZMBS ZYXWVUTSRQPO 1
	goto ready
  altfire:
    ZMBS A 0 a_playweaponsound("shotguy/attack")
	ZMBS BBB 0 a_firecustommissile("shotguyshrapnel", random(40, -40)*0.1, 0, 0, 0, 0, random(40, -40)*0.1)
    ZMBS B 1 bright
	ZMBS C 1 bright
	ZMBS DEFF 1
	ZMBS EGDHIJKLMNA 1
	ZMBS OPQRSTUVWXYZ 1
	ZMS2 A 3
	ZMBS ZYXWVUTSRQPO 1
	goto ready
  }
}

actor shotguyshrapnel : doomimpball
{
  radius 2
  height 4
  gravity 0.5
  damage (14)
  bouncecount 2
  speed 30
  bouncefactor 0.5
  wallbouncefactor 1.0
  renderstyle normal
  alpha 1.0
  scale 0.2
  damagetype "monster"
  decal bulletchip
  +bounceonwalls
  +bounceonceilings
  +bounceonfloors
  +forcexybillboard
  -nogravity
  seesound ""
  deathsound ""
  projectilekickback 25
  states
  {
  spawn:
    ZSHR ABCDEFGH 1 bright
	loop
  death:
    TNT1 A 0
	stop
  crash:
    TNT1 A 0 a_spawnitemex("fancybulletpuff")
	stop
  }
}